allprojects {
    repositories {
        mavenCentral()
    }
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
}

project.group = 'cn.virtuepay'
project.archivesBaseName = 'virtuepay-java'
project.version = '1.0.0'
project.sourceCompatibility = 1.8
project.targetCompatibility = 1.8

dependencies {
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'commons-codec:commons-codec:1.10'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'virtuepay-java'
                packaging 'jar'
                description 'VirtuePay Java SDK'
                url 'https://virtuepay.cn'

                scm {
                    connection 'scm:git:https://github.com/virtuepay/virtuepay-java.git'
                    developerConnection 'scm:git:https://github.com/virtuepay/virtuepay-java.git'
                    url 'https://github.com/virtuepay/virtuepay-java'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'developer'
                        name 'virtuepay developer team'
                        email 'develop@virtuepay.cn'
                    }
                }
            }
        }
    }
}